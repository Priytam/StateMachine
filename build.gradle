buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'java-library'

group 'com.github.priytam'
version "${STATE_MACHINE_VESRION}"
description = """\
Statemachine library with sync and async way of handling state
events, Also have feature of expirying an state.
------------------------------------------
Project version: ${version}
Gradle version: ${gradle.gradleVersion}
------------------------------------------
"""
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'concurrent', name: 'concurrent', version: '1.3.4'
    compile group: 'com.google.guava', name: 'guava', version: '28.1-jre'
    compile group: 'log4j', name: 'log4j', version: '1.2.16'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    repositories {
        repositories {
            mavenCentral()
            jcenter()
        }
    }
}

if (project.hasProperty('release')) {

    subprojects {

        task javadocJar(type: Jar) {
            classifier = 'javadoc'
            from javadoc
        }

        task sourcesJar(type: Jar) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        artifacts {
            archives javadocJar, sourcesJar
        }
    }

    allprojects {
        apply plugin: 'signing'
        apply plugin: 'maven'

        signing {
            sign configurations.archives
        }
        uploadArchives {
            repositories {
                mavenDeployer {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                        authentication(userName: ossrhUser, password: ossrhPassword)
                    }
                    pom.project {
                        name project.name
                        description project.description
                        packaging 'jar'
                        url 'https://github.com/Priytam/statemachine'

                        scm {
                            connection 'scm:https://github.com/Priytam/statemachine.git'
                            developerConnection 'scm:git:git@github.com:/priytam/statemachine.git'
                            url 'https://github.com/Priytam/statemachine.git'
                        }

                        licenses {
                            license {
                                name 'The MIT License (MIT)'
                                url 'http://opensource.org/licenses/MIT'
                                distribution 'repo'
                            }
                        }
                        developers {
                            developer {
                                id = 'pjpandey'
                                name = 'Priytam Jee Pandey'
                                email = 'mrpjpandey@gmail.com'
                            }
                        }
                    }
                }
            }
        }
    }
}

nexusStaging {
    username = ossrhUser
    password = ossrhPassword
}

/* use this command to publish library ./gradlew build -Prelease uploadArchives */
